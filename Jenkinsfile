pipeline {
    agent any

    parameters {
        string(name: 'BRANCH', defaultValue: 'master', description: 'Branch to deploy (master = prod, dev = staging)')
    }

    environment {
        APP_PATH    = '/var/www/portfolio'
        PHP_VERSION = '8.3'
        TMP_ENV_DIR = "/tmp/portfolio-env-${env.BUILD_NUMBER}"
        // Ensure jenkins uses its own SSH key for all git operations
        GIT_SSH_COMMAND = 'ssh -i /var/lib/jenkins/.ssh/id_ed25519 -o StrictHostKeyChecking=no'
    }

    stages {

        stage('Fetch Environment File') {
            steps {
                sh '''
                    mkdir -p ${TMP_ENV_DIR}
                    git clone --depth=1 git@github.com:granitebps/env.git ${TMP_ENV_DIR}
                '''
            }
        }

        stage('Deploy') {
            steps {
                script {
                    def deployDir = env.APP_PATH
                    def envFile   = '.prod.env'

                    if (params.BRANCH == 'dev') {
                        deployDir = '/var/www/portfolio-dev'
                        envFile   = '.dev.env'
                    }

                    sh """
                        cd ${deployDir}

                        # Clear stale cache before pulling
                        php${PHP_VERSION} artisan optimize:clear

                        # Pull latest code
                        git fetch origin ${params.BRANCH}
                        git reset --hard origin/${params.BRANCH}

                        # Install production dependencies
                        composer install --no-interaction --prefer-dist --optimize-autoloader --no-dev

                        # Update .env from private env repo
                        cp ${TMP_ENV_DIR}/portfolio/${envFile} ${deployDir}/.env

                        # Run migrations
                        php${PHP_VERSION} artisan migrate --force

                        # Warm caches
                        php${PHP_VERSION} artisan optimize

                        # Reload PHP-FPM to clear OPcache
                        sudo service php${PHP_VERSION}-fpm reload
                    """
                }
            }
        }
    }

    post {
        always {
            sh 'rm -rf ${TMP_ENV_DIR}'
            cleanWs()

            script {
                def mins     = (currentBuild.duration / 60000) as int
                def secs     = ((currentBuild.duration % 60000) / 1000) as int
                def duration = String.format('%02d:%02d', mins, secs)

                emailext(
                    to: 'granitebagas28@gmail.com',
                    subject: "Jenkins ${currentBuild.currentResult}: ${env.JOB_NAME} #${env.BUILD_NUMBER}",
                    body: """
                        <p><b>Status:</b> ${currentBuild.currentResult}</p>
                        <p><b>Branch:</b> ${params.BRANCH}</p>
                        <p><b>Build:</b> <a href="${env.BUILD_URL}">${currentBuild.fullDisplayName}</a></p>
                        <p><b>Duration:</b> ${duration} (mm:ss)</p>
                    """,
                    mimeType: 'text/html'
                )
            }
        }
    }
}
